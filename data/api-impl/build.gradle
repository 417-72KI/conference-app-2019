import dependencies.Dep

apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file('gradle/android.gradle')
android.libraryVariants.all {
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    it.generateBuildConfig.enabled = true
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')

        final def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
        final def iosPreset = (buildForDevice) ? presets.iosArm64 : presets.iosX64
        fromPreset(iosPreset, 'ios') {
            compilations.main.outputKinds('FRAMEWORK')
        }
    }
    sourceSets {
        commonMain.dependencies {
            api project(":data:api")
            implementation Dep.Ktor.clientCommon
            implementation Dep.Kotlin.serialization
        }
    }
}

android {
    defaultConfig {
        buildConfigField "String", "API_ENDPOINT", "\"https://droidkaigi2019-dev.appspot.com/api\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_ENDPOINT", "\"https://droidkaigi-api.appspot.com/2019/api\""
        }
    }
}

dependencies {

    implementation Dep.Kotlin.stdlibJvm
    implementation Dep.Ktor.clientAndroid
    implementation Dep.Ktor.jsonJvm
    implementation Dep.OkHttp.client
    implementation Dep.Stetho.stetho
    implementation Dep.Dagger.core
    implementation Dep.Dagger.androidSupport
    implementation Dep.OkHttp.loggingInterceptor
    kapt Dep.Dagger.compiler
    kapt Dep.Dagger.androidProcessor

    testImplementation Dep.Test.junit
    testImplementation Dep.Test.slf4j
}


