import com.android.build.gradle.LibraryExtension
import com.android.build.gradle.LibraryPlugin
import dependencies.Dep

apply from: file('dependencyGraph.gradle')

buildscript {
    ext.kotlin_version = '1.3.0-rc-145'
    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "http://kotlin.bintray.com/kotlin-dev" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.google.gms:google-services:4.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "http://kotlin.bintray.com/kotlin-dev" }
    }
    plugins.whenPluginAdded {
        if (it instanceof LibraryPlugin) {
            extensions.getByType(LibraryExtension).libraryVariants.all {
                // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
                it.generateBuildConfig.enabled = false
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task dependencyReport {
    doLast {
        def file = new File("project-dependencies.dot")
        file.delete()
        file << "digraph {\n"
        file << "splines=ortho\n"
        rootProject.childProjects.each { item ->
            def from = item.value
            from.configurations.compile.dependencies
                    .matching { it in ProjectDependency }
                    .each { to -> file << ("\"${from.name}\" -> \"${to.name}\"\n") }
        }
        file << "}\n"
    }
}
