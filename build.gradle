import com.android.build.gradle.AppPlugin
import com.android.build.gradle.LibraryExtension
import com.android.build.gradle.LibraryPlugin
import dependencies.Dep

apply from: file('dependencyGraph.gradle')

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "http://kotlin.bintray.com/kotlin-dev" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath Dep.Android.gradlePlugin
        classpath Dep.Kotlin.plugin
        classpath Dep.Kotlin.serializationPlugin
        classpath Dep.PlayServices.plugin
        classpath Dep.Arch.Navigation.safeArgsPlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "http://kotlin.bintray.com/kotlin-dev" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    plugins.whenPluginAdded {
        if (it instanceof LibraryPlugin) {
            extensions.getByType(LibraryExtension).libraryVariants.all {
                // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
                it.generateBuildConfig.enabled = false
            }
        }

        if (it instanceof AppPlugin || it instanceof LibraryPlugin) {
            project.with {
                configurations {
                    ktlint
                }

                dependencies {
                    ktlint "com.github.shyiko:ktlint:0.29.0"
                }

                task ktlint(type: JavaExec, group: "verification") {
                    description = 'Check Kotlin code style.'
                    args "--android", '--editorconfig', "${rootProject.projectDir}/.editorconfig", "--reporter=html,artifact=me.cassiano:ktlint-html-reporter:0.2.0,output=${buildDir}/ktlint/checkstyle.html", "--reporter=checkstyle,output=${buildDir}/ktlint/checkstyle.xml", 'src/main/**/*.kt', 'src/debug/**/*.kt', 'src/release/**/*.kt'
                    // https://github.com/shyiko/ktlint/blob/5fe8d0e203275a1a3337dc9777b50ec2a34a58df/ktlint/src/main/kotlin/com/github/shyiko/ktlint/Main.kt
                    main = 'com.github.shyiko.ktlint.Main'
                    classpath = configurations.ktlint
                }

                task ktlintFormat(type: JavaExec, group: "formatting") {
                    description = "Format Kotlin files based on code style."
                    classpath = configurations.ktlint
                    main = "com.github.shyiko.ktlint.Main"
                    args "--android", '-F', 'src/main/**/*.kt', 'src/debug/**/*.kt', 'src/release/**/*.kt'
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task dependencyReport {
    doLast {
        def file = new File("project-dependencies.dot")
        file.delete()
        file << "digraph {\n"
        file << "splines=ortho\n"
        rootProject.childProjects.each { item ->
            def from = item.value
            from.configurations.compile.dependencies
                    .matching { it in ProjectDependency }
                    .each { to -> file << ("\"${from.name}\" -> \"${to.name}\"\n") }
        }
        file << "}\n"
    }
}
